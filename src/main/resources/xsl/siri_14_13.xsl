<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:xs="http://www.w3.org/2001/XMLSchema"
    xmlns:soapenv="http://schemas.xmlsoap.org/soap/envelope/"
    xmlns:siri="http://www.siri.org.uk/siri"
    exclude-result-prefixes="xs" version="2.0">

<!-- This file contains mappings from SIRI 1.4 to the supported 1.3 of onebusaway.
        It also contains various "fixes" to the data received from Kolumbus (for now) -->

    <xsl:output indent="yes"/>
    <xsl:template match="node()|@*">
        <xsl:copy>
            <xsl:apply-templates select="node()|@*"/>
        </xsl:copy>
    </xsl:template>
        
    <xsl:template match="siri:*/@version">
        <xsl:attribute name="version">1.3</xsl:attribute>
    </xsl:template>    

    <!-- 1.4 node remove vehicle mode, insert FramedVehicleJourneyRef as it is missing from Kolumbus. Used by VM -->
    <xsl:template match="siri:MonitoredVehicleJourney/siri:VehicleMode">
        <xsl:comment>FramedVehicleJourneyRef has been generated by Rutebanken to circumvent validations errors and to provide a trip_id which might be wrong</xsl:comment>
        <xsl:element name="siri:FramedVehicleJourneyRef">
            <xsl:element name="siri:DataFrameRef">
                <xsl:value-of select="current-date()"/>
            </xsl:element>
            <xsl:element name="siri:DatedVehicleJourneyRef">
                <xsl:value-of select="../siri:CourseOfJourneyRef"/>
            </xsl:element>
        </xsl:element>
        
    </xsl:template>

    <!-- Remove vehicles with recorded time 1970 . Used by VM-->
    <xsl:template match="siri:VehicleActivity[siri:RecordedAtTime = '1970-01-01T01:00:00+01:00']" priority="1"/>
    
    <!-- Skip vehicles where they have no clue where they are (probably not in operation) . Used by VM -->
    <xsl:template match="siri:VehicleActivity[count(siri:MonitoredVehicleJourney/siri:VehicleLocation/siri:Latitude) = 0]" priority="0"/>
    
    <!-- Add publication window for alerts that does not contain this. Used by SX -->
    <xsl:template match="siri:PtSituationElement/siri:ValidityPeriod[1]">
        <xsl:copy-of select="../siri:ValidityPeriod"/>
        <xsl:copy-of select="siri:Repetitions"/>
        <xsl:if test="count(../siri:PublicationWindow) = 0">
            <xsl:for-each select="../siri:ValidityPeriod">
                <xsl:if test="(xs:dateTime(siri:StartTime) &lt; current-dateTime()) and (xs:dateTime(siri:EndTime) &gt; current-dateTime())">
                    <xsl:comment>PublicationWindow has been generated by Rutebanken to facilitate GTFS-RT mapping by onebusaway</xsl:comment>
                    <xsl:element name="PublicationWindow">
                        <xsl:copy-of select="current()/*"/>
                    </xsl:element>
                </xsl:if>
            </xsl:for-each>
                
        </xsl:if>
     
    </xsl:template>
    
    <!-- Only want match on first as the order of elements must be kept correct (ValidityPeriod(s), Repetitions and ValidityPeriod(s) -->
    <xsl:template match="siri:PtSituationElement/siri:ValidityPeriod[position() > 1]"/>
    
</xsl:stylesheet>
